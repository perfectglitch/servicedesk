plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.moowork.node' version '1.3.1'
}

apply plugin: 'base'

group = 'com.example.energia'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

node {
    download = true
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
}

task installAppDependencies(type: NpmTask) {
    workingDir = file("${project.projectDir}/app")
    args = ["install"]
}

task testApp(type: NpmTask) {
    workingDir = file("${project.projectDir}/app")
    args = ["run", "test"]
}

task buildApp(type: NpmTask, dependsOn: testApp) {
    workingDir = file("${project.projectDir}/app")
    args = ["run", "build"]
}

task packageApp(type: Copy, dependsOn: buildApp) {
    from 'app/build'
    into 'build/resources/main/public/.'
}

task stage(type: GradleBuild) {
    tasks = ['clean', 'installAppDependencies', 'packageApp', 'build']
}

installAppDependencies.mustRunAfter clean
packageApp.mustRunAfter installAppDependencies
build.mustRunAfter packageApp

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9'
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
