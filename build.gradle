plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

apply plugin: 'base'

group = 'com.example.energia'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

// Include integration test sources
sourceSets {
    integTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integTest/java')
        }
        resources.srcDir file('src/integTest/resources')
    }
}

// Make sure integration tests have access to dependencies
configurations {
    integTestCompile.extendsFrom testCompile
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom runtimeOnly
}

// Make integration tests run after unit tests
task integTest(type: Test) {
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn integTest

// Include React build output in jar after bundling it
processResources {
    from('app/build') {
        into 'public'
    }
    dependsOn 'app:bundle'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9'
    compileOnly 'org.projectlombok:lombok:1.18.8'

    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
